description: 'I would like you to implement a module named TopModule with the following
interface. All input and output ports are one bit unless otherwise
specified.

 - input  clk
 - input  reset
 - input  w
 - output z

The module should implement the state machine shown below:

  A (0) --1--> B
  A (0) --0--> A
  B (0) --1--> C
  B (0) --0--> D
  C (0) --1--> E
  C (0) --0--> D
  D (0) --1--> F
  D (0) --0--> A
  E (1) --1--> E
  E (1) --0--> D
  F (1) --1--> C
  F (1) --0--> D

Reset resets into state A and is synchronous active-high. Assume all
sequential logic is triggered on the positive edge of the clock.

Use separate always blocks for the state table and the state flip-flops.
Describe the FSM output, which is called _z_, using either continuous
assignment statement(s) or an always block (at your discretion). Assign
any state codes that you wish to use.'
interface: |-
  module RefModule (
    input clk,
    input reset,
    input w,
    output z
  );
  
    parameter A=0,B=1,C=2,D=3,E=4,F=5;
    reg[2:0] state, next;
  
    always @(posedge clk)
      if (reset) state <= A;
      else state <= next;
  
    always_comb begin
      case (state)
        A: next = w ? B : A;
        B: next = w ? C : D;
        C: next = w ? E : D;
        D: next = w ? F : A;
        E: next = w ? E : D;
        F: next = w ? C : D;
        default: next = 'x;
      endcase
    end
  
    assign z = (state == E) || (state == F);
  
  endmodule
bench_response: |
  module RefModule (
    input clk,
    input reset,
    input w,
    output z
  );
  
    parameter A=0,B=1,C=2,D=3,E=4,F=5;
    reg[2:0] state, next;
  
    always @(posedge clk)
      if (reset) state <= A;
      else state <= next;
  
    always_comb begin
      case (state)
        A: next = w ? B : A;
        B: next = w ? C : D;
        C: next = w ? E : D;
        D: next = w ? F : A;
        E: next = w ? E : D;
        F: next = w ? C : D;
        default: next = 'x;
      endcase
    end
  
    assign z = (state == E) || (state == F);
  
  endmodule
bench_test: |
  `timescale 1 ps/1 ps
  `define OK 12
  `define INCORRECT 13
  
  
  module stimulus_gen (
  	input clk,
  	output logic reset,
  	output logic w
  );
  
  	initial begin
  		repeat(200) @(negedge clk) begin
  			reset <= ($random & 'h1f) == 0;
  			w <= $random;
  		end
  		
  		#1 $finish;
  	end
  	
  endmodule
  
  module tb();
  
  	typedef struct packed {
  		int errors;
  		int errortime;
  		int errors_z;
  		int errortime_z;
  
  		int clocks;
  	} stats;
  	
  	stats stats1;
  	
  	
  	wire[511:0] wavedrom_title;
  	wire wavedrom_enable;
  	int wavedrom_hide_after_time;
  	
  	reg clk=0;
  	initial forever
  		#5 clk = ~clk;
  
  	logic reset;
  	logic w;
  	logic z_ref;
  	logic z_dut;
  
  	initial begin 
  		$dumpfile("wave.vcd");
  		$dumpvars(1, stim1.clk, tb_mismatch ,clk,reset,w,z_ref,z_dut );
  	end
  
  
  	wire tb_match;		// Verification
  	wire tb_mismatch = ~tb_match;
  	
  	stimulus_gen stim1 (
  		.clk,
  		.* ,
  		.reset,
  		.w );
  	RefModule good1 (
  		.clk,
  		.reset,
  		.w,
  		.z(z_ref) );
  		
  	TopModule top_module1 (
  		.clk,
  		.reset,
  		.w,
  		.z(z_dut) );
  
  	
  	bit strobe = 0;
  	task wait_for_end_of_timestep;
  		repeat(5) begin
  			strobe <= !strobe;  // Try to delay until the very end of the time step.
  			@(strobe);
  		end
  	endtask	
  
  	
  	final begin
  		if (stats1.errors_z) $display("Hint: Output '%s' has %0d mismatches. First mismatch occurred at time %0d.", "z", stats1.errors_z, stats1.errortime_z);
  		else $display("Hint: Output '%s' has no mismatches.", "z");
  
  		$display("Hint: Total mismatched samples is %1d out of %1d samples\n", stats1.errors, stats1.clocks);
  		$display("Simulation finished at %0d ps", $time);
  		$display("Mismatches: %1d in %1d samples", stats1.errors, stats1.clocks);
  	end
  	
  	// Verification: XORs on the right makes any X in good_vector match anything, but X in dut_vector will only match X.
  	assign tb_match = ( { z_ref } === ( { z_ref } ^ { z_dut } ^ { z_ref } ) );
  	// Use explicit sensitivity list here. @(*) causes NetProc::nex_input() to be called when trying to compute
  	// the sensitivity list of the @(strobe) process, which isn't implemented.
  	always @(posedge clk, negedge clk) begin
  
  		stats1.clocks++;
  		if (!tb_match) begin
  			if (stats1.errors == 0) stats1.errortime = $time;
  			stats1.errors++;
  		end
  		if (z_ref !== ( z_ref ^ z_dut ^ z_ref ))
  		begin if (stats1.errors_z == 0) stats1.errortime_z = $time;
  			stats1.errors_z = stats1.errors_z+1'b1; end
  
  	end
  
     // add timeout after 100K cycles
     initial begin
       #1000000
       $display("TIMEOUT");
       $finish();
     end
  
  endmodule
